WHITESPACE = _{ " " }
NEWLINE = _{ "\n" | "\r\n" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }

program = { SOI ~ (instruction ~ (NEWLINE | COMMENT)*)* ~ EOI }

instruction = _{ mov | add | sub | mul | div | jmp | cmp | label | org | inc }
mov = { "mov" ~ operand ~ "," ~ operand }
add = { "add" ~ operand ~ "," ~ operand }
sub = { "sub" ~ operand ~ "," ~ operand }
mul = { "mul" ~ operand }
div = { "div" ~ operand }
jmp = { "jmp" ~ name }
cmp = { "cmp" ~ operand ~ "," ~ operand }
org = { "org" ~ imm }
inc = { "inc" ~ operand }

name = { ASCII_ALPHA ~ ASCII_ALPHANUMERIC+ }
label = { name ~ ":" }
operand = _{ register | imm | mem | indirect }

/// Operand should be parsed into reg8/reg16/imm.
/// So register and number are defined as the silent rule.
register = _{ reg8 | reg16 }
reg16 = { "ax" | "bx" | "cx" | "dx" | "sp" | "bp" | "si" | "di" | "cs" | "ds" | "es" | "ss" }
reg8 = { "ah" | "al" | "bh" | "bl" | "ch" | "cl" | "dh" | "dl" }

/// Allow only HEX digit in assembly code
/// Atomic rule: No whitespace between 0x and others
/// Three hex digit form: 0xabcd, 0abcdh, 1abch
imm = @{ "0x" ~ ASCII_HEX_DIGIT+ | ASCII_HEX_DIGIT+ ~ "h" | ASCII_DIGIT+ }

// direct addressing: use only address such as [0a0h] or [1234h]
mem = _{ mem8 | mem16 }
mem8 = { "byte ptr" ~ memx }
mem16 = { "word ptr" ~ memx | memx }
memx = @{ "[" ~ imm ~ "]" }

// indirect addressing: use base/index register and address [bx + si + 1234h] or [bx + 10h]
indirect = _{ indirect8 | indirect16 }
indirect8 = { "byte ptr" ~ indirect_reg | "byte ptr" ~ indirect_disp }
indirect16 = { "word ptr" ~ indirect_reg | indirect_reg | "word ptr" ~ indirect_disp | indirect_disp }
indirect_reg = _{ "[" ~ base ~ "+" ~ index ~ "]" | "[" ~ base ~ "]" | "[" ~ index ~ "]" }
indirect_disp = _{ "[" ~ base ~ "+" ~ index ~ "+" ~ imm ~ "]" | "[" ~ base ~ "+" ~ imm ~ "]" | "[" ~ index ~ "+" ~ imm ~"]" }
base = { "bx" | "bp" }
index = { "si" | "di" }
